buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
    }

    lintOptions {
        disable 'GradleCompatible'
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    //implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/FlashRuntimeExtensions.jar')
    implementation files('libs/overrideAdobe.jar')

    implementation 'com.adobe.mobile:adobeMobileLibrary:4.18.0'
}

task packageAne {
    // Import Ant build structure
    ant.importBuild('../ANE/build/build.xml') {
        antTargetName ->
            'ant' + antTargetName
                    .capitalize()
                    .replace('ANE', 'Ane')
                    .replaceAll("( )([A-Za-z0-9])", { Object[] str -> str[2].toUpperCase() })
    }

    // Define tasks that should run
    dependsOn 'clean'
    dependsOn 'assemble'
    dependsOn 'antBuildAne'

    // Define task sequence and order
    tasks.findByName('assemble').mustRunAfter 'clean'
    tasks.findByName('antBuildAne').mustRunAfter 'assemble'
}

defaultTasks 'packageAne'
